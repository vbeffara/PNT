import Mathlib.Analysis.Calculus.Deriv.Support
import Mathlib.Analysis.Distribution.SchwartzSpace
import Mathlib.Order.Filter.ZeroAndBoundedAtFilter

open Real Complex MeasureTheory Filter Topology BoundedContinuousFunction SchwartzMap  BigOperators Set

attribute [fun_prop] Integrable Integrable.norm Integrable.const_mul Integrable.add Integrable.sub
attribute [fun_prop] AEStronglyMeasurable Continuous.aestronglyMeasurable
attribute [fun_prop] HasCompactSupport HasCompactSupport.smul_right HasCompactSupport.smul_right HasCompactSupport.mul_left

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {k n : ‚Ñï} {ùïú : Type*} [RCLike ùïú]

@[ext] structure CD (n : ‚Ñï) (E : Type*) [NormedAddCommGroup E] [NormedSpace ‚Ñù E] where
  toFun : ‚Ñù ‚Üí E
  smooth : ContDiff ‚Ñù n toFun

@[ext] structure CS (n : ‚Ñï) (E : Type*) [NormedAddCommGroup E] [NormedSpace ‚Ñù E] extends CD n E where
  compact : HasCompactSupport toFun

structure trunc extends (CS 2 ‚Ñù) where
  h3 : (Set.Icc (-1) (1)).indicator 1 ‚â§ toFun
  h4 : toFun ‚â§ Set.indicator (Set.Ioo (-2) (2)) 1

section lemmas

noncomputable def funscale {E : Type*} (g : ‚Ñù ‚Üí E) (R x : ‚Ñù) : E := g (R‚Åª¬π ‚Ä¢ x)

lemma contDiff_ofReal : ContDiff ‚Ñù ‚ä§ ofReal' := by
  have key x : HasDerivAt ofReal' 1 x := hasDerivAt_id x |>.ofReal_comp
  have key' : deriv ofReal' = fun _ => 1 := by ext x ; exact (key x).deriv
  refine contDiff_top_iff_deriv.mpr ‚ü®fun x => (key x).differentiableAt, ?_‚ü©
  simpa [key'] using contDiff_const

lemma tendsto_funscale {f : ‚Ñù ‚Üí E} (hf : ContinuousAt f 0) (x : ‚Ñù) :
    Tendsto (fun R => funscale f R x) atTop (ùìù (f 0)) :=
  hf.tendsto.comp (by simpa using tendsto_inv_atTop_zero.mul_const x)

@[fun_prop] nonrec lemma HasCompactSupport.iteratedDeriv {f : ‚Ñù ‚Üí E} (hf : HasCompactSupport f) :
    HasCompactSupport (iteratedDeriv n f) := by
  induction n with
  | zero => exact hf
  | succ n ih => simpa [iteratedDeriv_succ] using ih.deriv

end lemmas

namespace CD

variable {f : CD n E} {R x v : ‚Ñù}

instance : CoeFun (CD n E) (fun _ => ‚Ñù ‚Üí E) where coe f := f.toFun

instance : Coe (CD n ‚Ñù) (CD n ‚ÑÇ) where coe f := ‚ü®ofReal' ‚àò f, contDiff_ofReal.of_le le_top |>.comp f.smooth‚ü©

def of_le (f : CD n E) {m : ‚Ñï} (hm : m ‚â§ n) : CD m E := ‚ü®f, f.smooth.of_le (by simp [hm])‚ü©

def of_succ (f : CD (n + 1) E) : CD n E := ‚ü®f, f.smooth.of_succ‚ü©

instance : Coe (CD (n + 1) E) (CD n E) where coe f := f.of_succ

def neg (f : CD n E) : CD n E := ‚ü®-f, f.smooth.neg‚ü©

instance : Neg (CD n E) where neg := neg

def const_smul (R : ‚Ñù) (f : CD n E) : CD n E := ‚ü®R ‚Ä¢ f, f.smooth.const_smul R‚ü©

instance : HSMul ‚Ñù (CD n E) (CD n E) where hSMul := const_smul

@[simp] lemma smul_apply : (R ‚Ä¢ f) x = R ‚Ä¢ f x := rfl

@[continuity, fun_prop] lemma continuous (f : CD n E) : Continuous f := f.smooth.continuous

noncomputable nonrec def deriv (f : CD (n + 1) E) : CD n E := ‚ü®deriv f, (contDiff_succ_iff_deriv.mp f.smooth).2‚ü©

lemma hasDerivAt (f : CD (n + 1) E) (x : ‚Ñù) : HasDerivAt f (f.deriv x) x :=
  (f.smooth.differentiable (by simp)).differentiableAt.hasDerivAt

lemma deriv_apply {f : CD (n + 1) E} {x : ‚Ñù} : f.deriv x = _root_.deriv f x := rfl

lemma deriv_const_smul {f : CD (n + 1) E} : (R ‚Ä¢ f).deriv = R ‚Ä¢ f.deriv := by
  ext x ; exact (f.hasDerivAt x |>.const_smul R).deriv

noncomputable def scale (g : CD n E) (R : ‚Ñù) : CD n E := by
  by_cases R = 0
  ¬∑ exact ‚ü®0, contDiff_const‚ü©
  ¬∑ exact ‚ü®funscale g R, g.smooth.comp (contDiff_const.smul contDiff_id)‚ü©

lemma deriv_scale {f : CD (n + 1) E} : (f.scale R).deriv = R‚Åª¬π ‚Ä¢ f.deriv.scale R := by
  ext v ; by_cases hR : R = 0 <;> simp [hR, scale]
  ¬∑ simp [deriv, const_smul] ; exact deriv_const _ _
  ¬∑ exact ((f.hasDerivAt (R‚Åª¬π ‚Ä¢ v)).scomp v (by simpa using (hasDerivAt_id v).const_smul R‚Åª¬π)).deriv

@[simp] lemma deriv_scale' {f : CD (n + 1) E} : (f.scale R).deriv v = R‚Åª¬π ‚Ä¢ f.deriv (R‚Åª¬π ‚Ä¢ v) := by
  rw [deriv_scale] ; by_cases hR : R = 0 <;> simp [hR, scale, funscale]

lemma hasDerivAt_scale (f : CD (n + 1) E) (R x : ‚Ñù) :
    HasDerivAt (f.scale R) (R‚Åª¬π ‚Ä¢ f.deriv (R‚Åª¬π ‚Ä¢ x)) x := by
  simpa using hasDerivAt (f.scale R) x

lemma tendsto_scale (f : CD n E) (x : ‚Ñù) : Tendsto (fun R => f.scale R x) atTop (ùìù (f 0)) := by
  apply (tendsto_funscale f.continuous.continuousAt x).congr'
  filter_upwards [eventually_ne_atTop 0] with R hR ; simp [scale, hR]

def add (f g : CD n E) : CD n E := ‚ü®f + g, f.smooth.add g.smooth‚ü©

instance : Add (CD n E) where add := add

def mul (f g : CD n ùïú) : CD n ùïú where
  toFun := f * g
  smooth := f.smooth.mul g.smooth

instance : Mul (CD n ùïú) where mul := mul

nonrec lemma deriv_mul (f g : CD (n + 1) ùïú) : (f * g).deriv = f.deriv * g.of_succ + f.of_succ * g.deriv := by
  ext t
  have l1 : DifferentiableAt ‚Ñù f.toFun t := (f.smooth.differentiable (by simp)).differentiableAt
  have l2 : DifferentiableAt ‚Ñù g.toFun t := (g.smooth.differentiable (by simp)).differentiableAt
  exact deriv_mul l1 l2

def smul (f : CD n ‚Ñù) (g : CD n E) : CD n E := ‚ü®fun t => f t ‚Ä¢ g t, f.smooth.smul g.smooth‚ü©

instance : SMul (CD n ‚Ñù) (CD n E) where smul := smul

nonrec lemma deriv_smul (f : CD (n + 1) ‚Ñù) (g : CD (n + 1) E) :
    (f ‚Ä¢ g).deriv = f.of_succ ‚Ä¢ g.deriv + f.deriv ‚Ä¢ g.of_succ := by
  ext t
  have l1 : DifferentiableAt ‚Ñù f.toFun t := (f.smooth.differentiable (by simp)).differentiableAt
  have l2 : DifferentiableAt ‚Ñù g.toFun t := (g.smooth.differentiable (by simp)).differentiableAt
  exact deriv_smul l1 l2

noncomputable nonrec def iteratedDeriv (k : ‚Ñï) (f : CD (n + k) E) : CD n E := by
  refine ‚ü®iteratedDeriv k f, ?_‚ü©
  simpa [iteratedDeriv_eq_iterate] using f.smooth.iterate_deriv' n k

noncomputable def iteratedDeriv_of_le {n : ‚Ñï} ‚¶Ék : ‚Ñï‚¶Ñ (hk : k ‚â§ n) (f : CD n E) : CD (n - k) E := by
  refine ‚ü®_root_.iteratedDeriv k f.toFun, ?_‚ü©
  have := Nat.le.dest hk ; simp_rw [add_comm k] at this ; obtain ‚ü®l, rfl‚ü© := this ; simp
  simpa [iteratedDeriv_eq_iterate] using f.smooth.iterate_deriv' l k

nonrec lemma iteratedDeriv_succ {k : ‚Ñï} {f : CD (n + (k + 1)) E} :
    iteratedDeriv (k + 1) f = iteratedDeriv k f.deriv := by
  simp [iteratedDeriv, iteratedDeriv_succ'] ; rfl

nonrec lemma deriv_add (f g : CD (n + 1) E) : (f + g).deriv = f.deriv + g.deriv := by
  ext x
  apply deriv_add
  ¬∑ exact (f.smooth.differentiable (by simp)).differentiableAt
  ¬∑ exact (g.smooth.differentiable (by simp)).differentiableAt

lemma iteratedDeriv_add {k : ‚Ñï} {f g : CD (n + k) E} :
    (f + g).iteratedDeriv k = f.iteratedDeriv k + g.iteratedDeriv k := by
  induction k with
  | zero => rfl
  | succ k ih => simp_rw [iteratedDeriv_succ, deriv_add, ih]

lemma iteratedDeriv_add' {k : ‚Ñï} {f g : CD (n + k) E} {x : ‚Ñù} :
    (f + g).iteratedDeriv k x = f.iteratedDeriv k x + g.iteratedDeriv k x := by
  rw [iteratedDeriv_add] ; rfl

end CD

namespace CS

variable {f : CS n E} {R x v : ‚Ñù}

lemma ext_CD {f g : CS n E} (h : f.toCD = g.toCD) : f = g := by
  cases f ; cases g ; simp ; exact h

instance : CoeFun (CS n E) (fun _ => ‚Ñù ‚Üí E) where coe f := f.toFun

instance : Coe (CS n E) (CD n E) where coe := toCD

instance : Coe (CS n ‚Ñù) (CS n ‚ÑÇ) where coe f := ‚ü®f, f.compact.comp_left (g := ofReal') rfl‚ü©

nonrec def of_le (f : CS n E) {m : ‚Ñï} (hm : m ‚â§ n) : CS m E := ‚ü®f.of_le hm, f.compact‚ü©

nonrec def of_succ (f : CS (n + 1) E) : CS n E := f.of_le (by simp)

instance : Coe (CS (n + 1) E) (CS n E) where coe f := f.of_succ

@[simp] lemma neg_toFun : (-f.toCD).toFun = -(f.toFun) := rfl

def neg (f : CS n E) : CS n E := ‚ü®-f, by simpa [HasCompactSupport, tsupport] using f.compact‚ü©

instance : Neg (CS n E) where neg := neg

@[simp] lemma neg_apply {x : ‚Ñù} : (-f) x = - (f x) := rfl

def smul (R : ‚Ñù) (f : CS n E) : CS n E := ‚ü®R ‚Ä¢ f, f.compact.smul_left‚ü©

instance : HSMul ‚Ñù (CS n E) (CS n E) where hSMul := smul

@[simp] lemma smul_apply : (R ‚Ä¢ f) x = R ‚Ä¢ f x := rfl

noncomputable nonrec def deriv (f : CS (n + 1) E) : CS n E := ‚ü®f.deriv, f.compact.deriv‚ü©

nonrec lemma hasDerivAt (f : CS (n + 1) E) (x : ‚Ñù) : HasDerivAt f (f.deriv x) x := f.hasDerivAt x

lemma deriv_apply {f : CS (n + 1) E} {x : ‚Ñù} : f.deriv x = _root_.deriv f x := CD.deriv_apply

lemma deriv_smul {f : CS (n + 1) E} : (R ‚Ä¢ f).deriv = R ‚Ä¢ f.deriv := by
  ext x ; exact (f.hasDerivAt x |>.const_smul R).deriv

noncomputable nonrec def scale (g : CS n E) (R : ‚Ñù) : CS n E := by
  refine ‚ü®g.scale R, ?_‚ü©
  by_cases h : R = 0 <;> simp [CD.scale, h]
  ¬∑ simp [HasCompactSupport, tsupport]
  ¬∑ exact g.compact.comp_smul (inv_ne_zero h)

nonrec lemma deriv_scale {f : CS (n + 1) E} : (f.scale R).deriv = R‚Åª¬π ‚Ä¢ f.deriv.scale R := by
  apply ext_CD ; exact CD.deriv_scale

nonrec lemma of_succ_scale {f : CS (n + 1) E} : f.of_succ.scale R = (f.scale R).of_succ := by
  ext ; by_cases hR : R = 0 <;> simp [scale, CD.scale, of_succ, of_le, CD.of_le, hR]

@[simp] lemma deriv_scale' {f : CS (n + 1) E} : (f.scale R).deriv v = R‚Åª¬π ‚Ä¢ f.deriv (R‚Åª¬π ‚Ä¢ v) := CD.deriv_scale'

lemma hasDerivAt_scale (f : CS (n + 1) E) (R x : ‚Ñù) :
    HasDerivAt (f.scale R) (R‚Åª¬π ‚Ä¢ f.deriv (R‚Åª¬π ‚Ä¢ x)) x := CD.hasDerivAt_scale _ _ _

lemma tendsto_scale (f : CS n E) (x : ‚Ñù) : Tendsto (fun R => f.scale R x) atTop (ùìù (f 0)) := CD.tendsto_scale _ _

lemma bounded : ‚àÉ C, ‚àÄ v, ‚Äñf v‚Äñ ‚â§ C := by
  obtain ‚ü®x, hx‚ü© := (continuous_norm.comp f.continuous).exists_forall_ge_of_hasCompactSupport f.compact.norm
  refine ‚ü®_, hx‚ü©

@[simp] lemma bounded' : BddAbove (range fun v ‚Ü¶ ‚Äñf.toFun v‚Äñ) :=
  (f.compact.norm.isCompact_range (by fun_prop)).bddAbove

lemma bounded'_of_le (hk : k ‚â§ n) : BddAbove (range fun v ‚Ü¶ ‚ÄñiteratedDeriv k f v‚Äñ) := by
  apply IsCompact.bddAbove
  apply f.compact.iteratedDeriv.norm.isCompact_range
  exact f.smooth.continuous_iteratedDeriv k (by simp [hk]) |>.norm

lemma integrable (f : CS n E) : Integrable f := f.continuous.integrable_of_hasCompactSupport f.compact

lemma integrable_iteratedDeriv {n : ‚Ñï} (f : CS n E) : Integrable (iteratedDeriv n f) := by
  induction n with
  | zero => exact f.integrable
  | succ n ih => simpa [iteratedDeriv_succ'] using ih f.deriv

lemma integrable_iteratedDeriv_of_le {n : ‚Ñï} (f : CS n E) ‚¶Ék : ‚Ñï‚¶Ñ (hk : k ‚â§ n) : Integrable (iteratedDeriv k f) := by
  obtain ‚ü®m, rfl‚ü© := Nat.le.dest hk ; exact (f.of_le hk).integrable_iteratedDeriv

noncomputable def norm (f : CS n E) : ‚Ñù :=
  Finset.sup' (s := Finset.range (n + 1)) (by simp) (fun k => ‚®Ü v, ‚ÄñiteratedDeriv k f v‚Äñ)

noncomputable instance : Norm (CS n E) where norm := norm

nonrec lemma norm_nonneg : 0 ‚â§ ‚Äñf‚Äñ := by
  simp [Norm.norm, norm] ; use 0 ; simp
  apply (norm_nonneg (f 0)).trans
  apply le_ciSup (f := fun x => ‚Äñf x‚Äñ) bounded'

lemma le_norm (f : CS n E) (x : ‚Ñù) : ‚Äñf x‚Äñ ‚â§ ‚Äñf‚Äñ := by
  apply (le_ciSup bounded' x).trans
  exact Finset.le_sup' (b := 0) (fun k => ‚®Ü v, ‚ÄñiteratedDeriv k f v‚Äñ) (by simp)

lemma le_norm_of_le (f : CS n E) (hk : k ‚â§ n) (x : ‚Ñù) : ‚ÄñiteratedDeriv k f x‚Äñ ‚â§ ‚Äñf‚Äñ := by
  apply (le_ciSup (bounded'_of_le hk) x).trans
  refine Finset.le_sup' (b := k) (fun k => ‚®Ü v, ‚ÄñiteratedDeriv k f v‚Äñ) (by simp ; omega)

lemma norm_of_succ (f : CS (n + 1) E) : ‚Äñf.of_succ‚Äñ ‚â§ ‚Äñf‚Äñ := by
  simp_rw [Norm.norm, norm] ; apply Finset.sup'_mono ; simp

lemma norm_succ {f : CS (n + 1) E} : ‚Äñf‚Äñ = (‚®Ü v, ‚Äñf v‚Äñ) ‚äî ‚Äñf.deriv‚Äñ := by
  simp_rw [Norm.norm, norm, deriv, CD.deriv, ‚Üê iteratedDeriv_succ']
  let s : ‚Ñï ‚Ü™ ‚Ñï := ‚ü®fun n => n + 1, Nat.succ_injective‚ü©
  have l1 : _ = Finset.sup' (.range (n + 1)) _ ((fun k ‚Ü¶ ‚®Ü v, ‚ÄñiteratedDeriv (k + 1) f.toFun v‚Äñ)) :=
    @Finset.sup'_map ‚Ñù ‚Ñï ‚Ñï _ (.range (n + 1)) s (fun k => ‚®Ü v, ‚ÄñiteratedDeriv k f.toFun v‚Äñ) (by simp)
  have l2 : Finset.map s (Finset.range (n + 1)) = Finset.Ico 1 (n + 2) := by
    ext i ; simp [s] ; constructor
    ¬∑ rintro ‚ü®a, h1, h2‚ü© ; omega
    ¬∑ rintro ‚ü®h1, h2‚ü© ; use i - 1 ; omega
  have l3 : insert 0 (Finset.Ico 1 (n + 2)) = Finset.range (n + 2) := by ext i ; simp ; omega
  simp [‚Üê l1, l2, ‚Üê l3]

lemma norm_deriv (f : CS (n + 1) E) : ‚Äñf.deriv‚Äñ ‚â§ ‚Äñf‚Äñ := by simp [norm_succ]

end CS

namespace trunc

instance : CoeFun trunc (fun _ => ‚Ñù ‚Üí ‚Ñù) where coe f := f.toFun

instance : Coe trunc (CS 2 ‚Ñù) where coe := trunc.toCS

lemma nonneg (g : trunc) (x : ‚Ñù) : 0 ‚â§ g x := (Set.indicator_nonneg (by simp) x).trans (g.h3 x)

lemma le_one (g : trunc) (x : ‚Ñù) : g x ‚â§ 1 := (g.h4 x).trans <| Set.indicator_le_self' (by simp) x

lemma zero (g : trunc) : g =·∂†[ùìù 0] 1 := by
  have : Set.Icc (-1) 1 ‚àà ùìù (0 : ‚Ñù) := by apply Icc_mem_nhds <;> linarith
  exact eventually_of_mem this (fun x hx => le_antisymm (g.le_one x) (by simpa [hx] using g.h3 x))

@[simp] lemma zero_at {g : trunc} : g 0 = 1 := g.zero.eq_of_nhds

end trunc

@[ext] structure W1 (n : ‚Ñï) (E : Type*) [NormedAddCommGroup E] [NormedSpace ‚Ñù E] extends CD n E where
  integrable ‚¶Ék‚¶Ñ (hk : k ‚â§ n) : Integrable (toCD.iteratedDeriv_of_le hk)

abbrev W21 := W1 2 ‚ÑÇ

namespace W1

instance : CoeFun (W1 n E) (fun _ => ‚Ñù ‚Üí E) where coe f := f.toFun

instance : Coe (W1 n E) (CD n E) where coe := toCD

@[fun_prop] lemma integrable' (f : W1 n E) : Integrable f := by
  exact f.integrable (k := 0) (by simp)

@[fun_prop, continuity] lemma continuous (f : W1 n E) : Continuous f := f.smooth.continuous

lemma differentiable (f : W1 (n + 1) E) : Differentiable ‚Ñù f :=
  f.smooth.differentiable (by simp)

lemma iteratedDeriv_sub {f g : ‚Ñù ‚Üí E} (hf : ContDiff ‚Ñù n f) (hg : ContDiff ‚Ñù n g) :
    iteratedDeriv n (f - g) = iteratedDeriv n f - iteratedDeriv n g := by
  induction n generalizing f g with
  | zero => rfl
  | succ n ih =>
    have hf' : ContDiff ‚Ñù n (deriv f) := hf.iterate_deriv' n 1
    have hg' : ContDiff ‚Ñù n (deriv g) := hg.iterate_deriv' n 1
    have hfg : deriv (f - g) = deriv f - deriv g := by
      ext x ; apply deriv_sub
      ¬∑ exact (hf.differentiable (by simp)).differentiableAt
      ¬∑ exact (hg.differentiable (by simp)).differentiableAt
    simp_rw [iteratedDeriv_succ', ‚Üê ih hf' hg', hfg]

lemma iteratedDeriv_add {f g : ‚Ñù ‚Üí E} (hf : ContDiff ‚Ñù n f) (hg : ContDiff ‚Ñù n g) :
    iteratedDeriv n (f + g) = iteratedDeriv n f + iteratedDeriv n g := by
  induction n generalizing f g with
  | zero => rfl
  | succ n ih =>
    have hf' : ContDiff ‚Ñù n (deriv f) := hf.iterate_deriv' n 1
    have hg' : ContDiff ‚Ñù n (deriv g) := hg.iterate_deriv' n 1
    have hfg : deriv (f + g) = deriv f + deriv g := by
      ext x ; apply deriv_add
      ¬∑ exact (hf.differentiable (by simp)).differentiableAt
      ¬∑ exact (hg.differentiable (by simp)).differentiableAt
    simp_rw [iteratedDeriv_succ', ‚Üê ih hf' hg', hfg]

noncomputable def deriv (f : W1 (n + 1) E) : W1 n E where
  toCD := f.toCD.deriv
  integrable k hk := by
    simpa [CD.iteratedDeriv_of_le, CD.deriv, ‚Üê iteratedDeriv_succ'] using f.integrable (Nat.succ_le_succ hk)

lemma hasDerivAt (f : W1 (n + 1) E) (x : ‚Ñù) : HasDerivAt f (f.deriv x) x := f.toCD.hasDerivAt _

def sub (f g : W1 n E) : W1 n E where
  toFun := f - g
  smooth := f.smooth.sub g.smooth
  integrable k hk := by
    have hf : ContDiff ‚Ñù k f := f.smooth.of_le (by simp [hk])
    have hg : ContDiff ‚Ñù k g := g.smooth.of_le (by simp [hk])
    simpa [CD.iteratedDeriv_of_le, iteratedDeriv_sub hf hg] using (f.integrable hk).sub (g.integrable hk)

instance : Sub (W1 n E) where sub := sub

@[simp] lemma sub_apply (f g : W1 n E) (x : ‚Ñù) : (f - g) x = f x - g x := rfl

lemma integrable_iteratedDeriv_Schwarz {f : ùì¢(‚Ñù, ‚ÑÇ)} : Integrable (iteratedDeriv n f) := by
  induction n generalizing f with
  | zero => exact f.integrable
  | succ n ih => simpa [iteratedDeriv_succ'] using ih (f := SchwartzMap.derivCLM ‚Ñù f)

def of_Schwartz (f : ùì¢(‚Ñù, ‚ÑÇ)) : W1 n ‚ÑÇ where
  toFun := f
  smooth := f.smooth n
  integrable _ _ := integrable_iteratedDeriv_Schwarz

instance : Coe (CS n E) (W1 n E) where coe f := ‚ü®f.toCD, f.integrable_iteratedDeriv_of_le‚ü©

def smul (g : CS n ‚Ñù) (f : W1 n E) : W1 n E := by
  refine ‚ü®g.toCD ‚Ä¢ f.toCD, ?_‚ü©
  intro k hk
  obtain ‚ü®l, rfl‚ü© : ‚àÉ l, l + k = n := by simpa [add_comm k] using Nat.le.dest hk
  apply Continuous.integrable_of_hasCompactSupport
  ¬∑ exact (g.toCD ‚Ä¢ f.toCD).iteratedDeriv k |>.continuous
  ¬∑ exact g.compact.smul_right.iteratedDeriv

instance : SMul (CS n ‚Ñù) (W1 n E) where smul := smul

noncomputable def L1_norm (f : ‚Ñù ‚Üí E) : ‚Ñù := ‚à´ v, ‚Äñf v‚Äñ

lemma L1_norm_add {f g : ‚Ñù ‚Üí E} (hf : Integrable f) (hg : Integrable g) :
    L1_norm (f + g) ‚â§ L1_norm f + L1_norm g := by
  rw [L1_norm, L1_norm, L1_norm, ‚Üê integral_add' hf.norm hg.norm]
  apply integral_mono (by fun_prop) (by fun_prop) ; intro t ; simp ; apply norm_add_le

lemma L1_norm_sub {f g : ‚Ñù ‚Üí E} (hf : Integrable f) (hg : Integrable g) :
    L1_norm (f - g) ‚â§ L1_norm f + L1_norm g := by
  rw [L1_norm, L1_norm, L1_norm, ‚Üê integral_add' hf.norm hg.norm]
  apply integral_mono (by fun_prop) (by fun_prop) ; intro t ; simp ; apply norm_sub_le

noncomputable def norm1 (f : W1 n E) : ‚Ñù := L1_norm ‚áëf

lemma norm1_nonneg (f : W1 n E) : 0 ‚â§ norm1 f := by
  rw [norm1, L1_norm] ; positivity

noncomputable def norm (n : ‚Ñï) (f : ‚Ñù ‚Üí E) : ‚Ñù :=
  ‚àë k in Finset.range (n + 1), L1_norm (iteratedDeriv k f)

noncomputable instance : Norm (W1 n E) where norm f := norm n f

@[simp] lemma norm_of_zero (f : W1 0 E) : ‚Äñf‚Äñ = L1_norm f := by simp [Norm.norm, norm]

lemma norm_nonneg {f : W1 n E} : 0 ‚â§ ‚Äñf‚Äñ := by
  simp [Norm.norm, norm, L1_norm] ; positivity

lemma norm_succ (f : W1 (n + 1) E) : ‚Äñf‚Äñ = norm1 f + ‚Äñf.deriv‚Äñ := by
  simp [Norm.norm, norm, norm1, deriv, CD.deriv, ‚Üê iteratedDeriv_succ', Finset.sum_range_succ' _ (n + 1)] ; ring

lemma integral_norm_le_norm (f : W1 n E) : norm1 f ‚â§ ‚Äñf‚Äñ := by
  have l1 i (_ : i ‚àà Finset.range (n + 1)) : 0 ‚â§ ‚à´ (v : ‚Ñù), ‚ÄñiteratedDeriv i f.toFun v‚Äñ := by positivity
  have l2 : 0 ‚àà Finset.range (n + 1) := by simp
  exact Finset.single_le_sum l1 l2

lemma norm_deriv (f : W1 (n + 1) E) : ‚Äñf.deriv‚Äñ ‚â§ ‚Äñf‚Äñ := by
  rw [norm_succ] ; linarith [norm1_nonneg f]

lemma norm_mul0 (g : CS n ‚Ñù) (f : W1 n E) : norm1 (g ‚Ä¢ f) ‚â§ ‚Äñg‚Äñ * norm1 f := by
  convert_to ‚à´ v, ‚Äñg v ‚Ä¢ f v‚Äñ ‚â§ ‚Äñg‚Äñ * (‚à´ v, ‚Äñf v‚Äñ) using 0
  rw [‚Üê integral_mul_left] ; refine integral_mono (g ‚Ä¢ f).integrable'.norm (by fun_prop) ?_
  intro v ; simp [norm_smul] ; gcongr ; exact g.le_norm v

def of_succ (f : W1 (n + 1) E) : W1 n E := ‚ü®f.toCD, fun k hk => f.integrable (by omega)‚ü©

instance : Coe (W1 (n + 1) E) (W1 n E) where coe := of_succ

lemma norm_of_succ (f : W1 (n + 1) E) : ‚Äñf.of_succ‚Äñ ‚â§ ‚Äñf‚Äñ := by
  simp_rw [Norm.norm, norm, L1_norm] ; apply Finset.sum_le_sum_of_subset_of_nonneg (by simp)
  rintro i - - ; positivity

def add (f g : W1 n E) : W1 n E := by
  refine ‚ü®f.toCD + g.toCD, fun k hk => ?_‚ü©
  have l1 := f.integrable hk
  have l2 := g.integrable hk
  have l3 := l1.add l2
  convert l3 ; ext x ; simp [CD.iteratedDeriv_of_le]
  have := Nat.le.dest hk ; simp_rw [add_comm k] at this ; obtain ‚ü®l, rfl‚ü© := this
  exact @CD.iteratedDeriv_add' E _ _ l k f g x

instance : Add (W1 n E) where add := add

@[simp] lemma add_apply (f g : W1 n E) (x : ‚Ñù) : (f + g) x = f x + g x := rfl

nonrec lemma deriv_sub (f g : W1 (n + 1) E) : (f - g).deriv = f.deriv - g.deriv := by
  ext x ; exact deriv_sub f.differentiable.differentiableAt g.differentiable.differentiableAt

lemma deriv_smul {g : CS (n + 1) ‚Ñù} {f : W1 (n + 1) E} :
    (g ‚Ä¢ f).deriv = g.of_succ ‚Ä¢ f.deriv + g.deriv ‚Ä¢ f.of_succ := by
  ext x ; apply _root_.deriv_smul
  ¬∑ exact g.smooth.differentiable (by simp) |>.differentiableAt
  ¬∑ exact f.smooth.differentiable (by simp) |>.differentiableAt

lemma norm_add_le {f g : W1 n E} : ‚Äñf + g‚Äñ ‚â§ ‚Äñf‚Äñ + ‚Äñg‚Äñ := by
  simp [Norm.norm, norm, ‚Üê Finset.sum_add_distrib] ; apply Finset.sum_le_sum ; intro k hk
  have lk : k ‚â§ n := by simp at hk ; omega
  have l1 : ContDiff ‚Ñù k f := by apply f.smooth.of_le ; simp [lk]
  have l2 : ContDiff ‚Ñù k g := by apply g.smooth.of_le ; simp [lk]
  have l3 : Integrable (iteratedDeriv k f) := by apply f.integrable lk
  have l4 : Integrable (iteratedDeriv k g) := by apply g.integrable lk
  change L1_norm (iteratedDeriv k (‚áëf + ‚áëg)) ‚â§ _
  rw [iteratedDeriv_add l1 l2] ; apply L1_norm_add l3 l4

lemma norm_sub_le {f g : W1 n E} : ‚Äñf - g‚Äñ ‚â§ ‚Äñf‚Äñ + ‚Äñg‚Äñ := by
  simp [Norm.norm, norm, ‚Üê Finset.sum_add_distrib] ; apply Finset.sum_le_sum ; intro k hk
  have lk : k ‚â§ n := by simp at hk ; omega
  have l1 : ContDiff ‚Ñù k f := by apply f.smooth.of_le ; simp [lk]
  have l2 : ContDiff ‚Ñù k g := by apply g.smooth.of_le ; simp [lk]
  have l3 : Integrable (iteratedDeriv k f) := by apply f.integrable lk
  have l4 : Integrable (iteratedDeriv k g) := by apply g.integrable lk
  change L1_norm (iteratedDeriv k (‚áëf - ‚áëg)) ‚â§ _
  rw [iteratedDeriv_sub l1 l2] ; apply L1_norm_sub l3 l4

theorem norm_mul (g : CS n ‚Ñù) (f : W1 n E) : ‚Äñg ‚Ä¢ f‚Äñ ‚â§ (2 ^ (n + 1) - 1) * (‚Äñg‚Äñ * ‚Äñf‚Äñ) := by
  induction n with
  | zero => norm_num ; simpa [Norm.norm, norm] using norm_mul0 g f
  | succ n ih =>
    have l1 : (0 : ‚Ñù) ‚â§ 2 ^ (n + 1) - 1 := by simp ; norm_cast ; apply Nat.one_le_pow'
    have key1 : norm1 (g ‚Ä¢ f) ‚â§ ‚Äñg‚Äñ * ‚Äñf‚Äñ := by
      apply norm_mul0 g f |>.trans
      have := integral_norm_le_norm f
      gcongr ; apply CS.norm_nonneg
    have key2 : ‚ÄñCS.of_succ g ‚Ä¢ deriv f‚Äñ ‚â§ (2 ^ (n + 1) - 1) * (‚Äñg‚Äñ * ‚Äñf‚Äñ) := by
      apply ih g.of_succ f.deriv |>.trans
      have := f.norm_deriv
      have := g.norm_of_succ
      gcongr ; apply norm_nonneg ; apply CS.norm_nonneg
    have key3 : ‚ÄñCS.deriv g ‚Ä¢ f.of_succ‚Äñ ‚â§ (2 ^ (n + 1) - 1) * (‚Äñg‚Äñ * ‚Äñf‚Äñ) := by
      apply ih g.deriv f.of_succ |>.trans
      have := f.norm_of_succ
      have := g.norm_deriv
      gcongr ; apply norm_nonneg ; apply CS.norm_nonneg
    have key4 : ‚Äñ(g ‚Ä¢ f).deriv‚Äñ ‚â§ (2 ^ (n + 2) - 2) * (‚Äñg‚Äñ * ‚Äñf‚Äñ) := by
      rw [deriv_smul] ; apply norm_add_le.trans
      convert add_le_add key2 key3 using 1 ; simp [pow_succ] ; ring
    rw [norm_succ] ; convert add_le_add key1 key4 using 1 ; simp [pow_succ] ; ring

lemma approx0 (f : W1 n E) (g : CS n ‚Ñù) (hg : g 0 = 1) :
    Tendsto (fun R ‚Ü¶ norm1 (f - CS.scale g R ‚Ä¢ f)) atTop (ùìù 0) := by

  let F R x := ‚Äñ(f - CS.scale g R ‚Ä¢ f) x‚Äñ
  let bound x := (1 + ‚Äñg‚Äñ) * ‚Äñf x‚Äñ
  have l1 : ‚àÄ·∂† (R : ‚Ñù) in atTop, AEStronglyMeasurable (F R) volume := by
    apply eventually_of_forall ; intro R
    exact Continuous.aestronglyMeasurable (by continuity)
  have l2 : ‚àÄ·∂† R in atTop, ‚àÄ·µê x, ‚ÄñF R x‚Äñ ‚â§ bound x := by
    filter_upwards [eventually_ne_atTop 0] with R hR
    apply eventually_of_forall ; intro x
    convert_to ‚Äñf x - (CS.scale g R ‚Ä¢ f) x‚Äñ ‚â§ ‚Äñf x‚Äñ + ‚Äñg‚Äñ * ‚Äñf x‚Äñ
    ¬∑ simp [F]
    ¬∑ simp [bound] ; ring
    apply (_root_.norm_sub_le _ _).trans ; gcongr
    change ‚ÄñCS.scale g R x ‚Ä¢ f x‚Äñ ‚â§ ‚Äñg‚Äñ * ‚Äñf.toFun x‚Äñ ; simp [norm_smul] ; gcongr
    simpa [CS.scale, CD.scale, hR, funscale] using CS.le_norm g (R‚Åª¬π * x)
  have l3 : Integrable bound volume := f.integrable'.norm.const_mul _
  have l4 : ‚àÄ·µê (a : ‚Ñù), Tendsto (fun n ‚Ü¶ F n a) atTop (ùìù 0) := by
    apply eventually_of_forall ; intro x
    simpa [hg] using (((g.tendsto_scale x).smul_const (f x)).const_sub (f x)).norm
  simpa using tendsto_integral_filter_of_dominated_convergence bound l1 l2 l3 l4

theorem W1_approximation (f : W1 n E) (g : CS n ‚Ñù) (hg : g 0 = 1) :
    Tendsto (fun R => ‚Äñf - g.scale R ‚Ä¢ f‚Äñ) atTop (ùìù 0) := by

  induction n with
  | zero => simpa using approx0 f g hg
  | succ n ih =>
    simp_rw [norm_succ] ; apply ZeroAtFilter.add (approx0 f g hg)
    simp_rw [deriv_sub, deriv_smul]
    convert_to ZeroAtFilter atTop fun R ‚Ü¶
        ‚Äñ(deriv f - CS.of_succ (CS.scale g R) ‚Ä¢ deriv f) - CS.deriv (CS.scale g R) ‚Ä¢ of_succ f‚Äñ
        using 1
    ¬∑ ext R ; congr 1 ; ext x ; simp [sub_sub]
    simp_rw [‚Üê CS.of_succ_scale, CS.deriv_scale]
    have key1 := ih f.deriv g.of_succ hg
    sorry

end W1

namespace W21

variable {f : W21}

noncomputable def norm (f : ‚Ñù ‚Üí ‚ÑÇ) : ‚Ñù :=
    (‚à´ v, ‚Äñf v‚Äñ) + (4 * œÄ ^ 2)‚Åª¬π * (‚à´ v, ‚Äñderiv (deriv f) v‚Äñ)

lemma norm_nonneg {f : ‚Ñù ‚Üí ‚ÑÇ} : 0 ‚â§ norm f :=
  add_nonneg (integral_nonneg (fun t => by simp))
    (mul_nonneg (by positivity) (integral_nonneg (fun t => by simp)))

noncomputable instance : Norm W21 where norm f := norm f

noncomputable instance : Coe ùì¢(‚Ñù, ‚ÑÇ) W21 where coe := W1.of_Schwartz

instance : Coe (CS 2 ‚ÑÇ) W21 where coe := fun f => f

def mul_CSC_W21 (g : CS 2 ‚ÑÇ) (f : W21) : CS 2 ‚ÑÇ := ‚ü®‚ü®g * f, g.smooth.mul f.smooth‚ü©, g.compact.mul_right‚ü©

instance : HMul (CS 2 ‚ÑÇ) W21 (CS 2 ‚ÑÇ) where hMul := mul_CSC_W21

noncomputable instance : HMul (CS 2 ‚Ñù) W21 (CS 2 ‚ÑÇ) where
  hMul g f := by
    refine ‚ü®g * f, ?_‚ü©
    apply HasCompactSupport.mul_right
    exact @HasCompactSupport.comp_left ‚Ñù ‚Ñù ‚ÑÇ _ _ _ ofReal' g g.compact rfl
end W21

lemma approx_aux1 {f : ‚Ñù ‚Üí E} {g : ‚Ñù ‚Üí ‚Ñù} (h1 : Integrable f) (h2 : ‚àÄ x, |g x| ‚â§ 1)
    (h3 : Continuous g) (h4 : g 0 = 1) :
    Tendsto (fun R => ‚à´ x, funscale g R x ‚Ä¢ f x) atTop (ùìù (‚à´ x, f x)) := by

  let F R x : E := funscale g R x ‚Ä¢ f x
  have l1 : ‚àÄ·∂† R in atTop, AEStronglyMeasurable (F R) := by
    apply eventually_of_forall ; intro R
    exact (h3.comp (by continuity)).aestronglyMeasurable.smul h1.1
  have l2 : ‚àÄ·∂† R in atTop, ‚àÄ·µê x, ‚ÄñF R x‚Äñ ‚â§ ‚Äñf x‚Äñ := by
    apply eventually_of_forall ; intro R ; apply eventually_of_forall ; intro x
    simp [F, funscale, norm_smul]
    convert_to _ ‚â§ 1 * ‚Äñf x‚Äñ ; simp
    have := h2 (R‚Åª¬π * x) ; gcongr
  have l4 : ‚àÄ·µê x, Tendsto (fun n ‚Ü¶ F n x) atTop (ùìù (f x)) := by
    apply eventually_of_forall ; intro x
    simpa [h4] using (tendsto_funscale h3.continuousAt x).smul_const (f x)
  exact tendsto_integral_filter_of_dominated_convergence _ l1 l2 h1.norm l4

lemma approx_aux2 {f : ‚Ñù ‚Üí E} {g : ‚Ñù ‚Üí ‚Ñù} (h1 : Integrable f)
    (h2 : ‚àÄ x, g x ‚â§ 1) (h2' : ‚àÄ x, 0 ‚â§ g x) (h3 : Continuous g) (h4 : g 0 = 1) :
    Tendsto (fun R => ‚à´ x, ‚Äñ(1 - funscale g R x) ‚Ä¢ f x‚Äñ) atTop (ùìù 0) := by

  let F R x : ‚Ñù := ‚Äñ(1 - funscale g R x) ‚Ä¢ f x‚Äñ
  have l1 : ‚àÄ·∂† R in atTop, AEStronglyMeasurable (F R) := by
    apply eventually_of_forall ; intro R
    exact ((aestronglyMeasurable_const.sub ((h3.comp (by continuity)).aestronglyMeasurable)).smul h1.1).norm
  have l2 : ‚àÄ·∂† R in atTop, ‚àÄ·µê x, ‚ÄñF R x‚Äñ ‚â§ ‚Äñf x‚Äñ := by
    apply eventually_of_forall ; intro R ; apply eventually_of_forall ; intro x
    convert_to |1 - g (R‚Åª¬π * x)| * ‚Äñf x‚Äñ ‚â§ 1 * ‚Äñf x‚Äñ ; simp [F, funscale, norm_smul] ; simp
    gcongr ; rw [abs_le] ; constructor <;> linarith [h2 (R‚Åª¬π * x), h2' (R‚Åª¬π * x)]
  have l4 : ‚àÄ·µê x, Tendsto (fun n ‚Ü¶ F n x) atTop (ùìù 0) := by
    apply eventually_of_forall ; intro x
    simpa [h4] using tendsto_funscale h3.continuousAt x |>.const_sub 1 |>.smul_const (f x) |>.norm
  simpa [F] using tendsto_integral_filter_of_dominated_convergence _ l1 l2 h1.norm l4

#exit

theorem W21_approximation (f : W21) (g : trunc) :
    Tendsto (fun R => ‚Äñf - g.scale R ‚Ä¢ f‚Äñ) atTop (ùìù 0) := by

  -- Setup
  let G R : CS 2 ‚Ñù := g.scale R ; let h R v := 1 - G R v
  convert_to Tendsto (fun R => W21.norm (fun v => h R v * f v)) atTop (ùìù 0)
  ¬∑ ext R ; change W21.norm _ = _ ; congr ; ext v ; simp [h, sub_mul] ; rfl

  -- Take care of the first piece
  rw [show (0 : ‚Ñù) = 0 + ((4 * œÄ ^ 2)‚Åª¬π : ‚Ñù) * 0 by simp]
  have piece_1 : Tendsto (fun R ‚Ü¶ ‚à´ v, ‚Äñh R v * f v‚Äñ) atTop (ùìù 0) := by
    apply approx_aux2 f.integrable' g.le_one g.nonneg g.continuous g.zero_at |>.congr'
    filter_upwards [eventually_ne_atTop 0] with R hR ; simp [h, G, CS.scale, CD.scale, hR]
  refine piece_1.add (Tendsto.const_mul _ ?_) ; clear piece_1

  -- Definitions
  let f' := f.deriv ; let f'' := f'.deriv
  let G' R := (G R).deriv ; let G'' R := (G' R).deriv
  let F R v := ‚Äñ- G'' R v * f v + 2 * -G' R v * f' v + h R v * f'' v‚Äñ

  -- Proof
  convert_to Tendsto (fun R ‚Ü¶ ‚à´ (v : ‚Ñù), F R v) atTop (ùìù 0)
  ¬∑ ext R ; congr ; ext v ; congr ; apply HasDerivAt.deriv
    have dh v : HasDerivAt (h R) (-G' R v) v := by
      simpa [G', G] using (g : CS 2 ‚Ñù).hasDerivAt_scale R v |>.const_sub 1
    have l5 := ((G' R).hasDerivAt v).ofReal_comp.neg.mul (f.hasDerivAt v)
    have l7 := (dh v).ofReal_comp.mul (f'.hasDerivAt v)
    have d1 : deriv _ = _ := funext (fun v => ((dh v).ofReal_comp.mul (f.hasDerivAt v)).deriv)
    rw [d1] ; convert (l5.add l7) using 1 <;> simp [h] ; ring_nf

  obtain ‚ü®c1, mg'‚ü© := (g : CS 2 ‚Ñù).deriv.bounded ; obtain ‚ü®c2, mg''‚ü© := (g : CS 2 ‚Ñù).deriv.deriv.bounded
  let bound v := c2 * ‚Äñf v‚Äñ + 2 * c1 * ‚Äñf' v‚Äñ + ‚Äñf'' v‚Äñ

  have e1 : ‚àÄ·∂† (n : ‚Ñù) in atTop, AEStronglyMeasurable (F n) volume := by
    apply eventually_of_forall ; intro R ; apply Continuous.aestronglyMeasurable ; fun_prop

  have e2 : ‚àÄ·∂† R in atTop, ‚àÄ·µê (a : ‚Ñù), ‚ÄñF R a‚Äñ ‚â§ bound a := by
    filter_upwards [eventually_ge_atTop 1] with R hR
    apply eventually_of_forall ; intro v
    have e1 : 0 ‚â§ R := by linarith
    have e2 : R‚Åª¬π ‚â§ 1 := inv_le_of_inv_le (by linarith) (by simpa using hR)
    have e3 : R ‚â† 0 := by linarith
    have hc1 : |G' R v| ‚â§ c1 := by
      simp [G', G, CS.deriv_scale, abs_mul, abs_inv, abs_eq_self.mpr e1] ; simp [CS.scale, CD.scale, funscale, e3]
      simpa using mul_le_mul e2 (mg' _) (norm_nonneg _) zero_le_one
    have hc2 : |G'' R v| ‚â§ c2 := by
      simp [G'', G', G, CS.deriv_scale, CS.deriv_smul, abs_mul, abs_inv, abs_eq_self.mpr e1]
      convert_to _ ‚â§ 1 * (1 * c2) ; simp
      gcongr ; simp [CS.scale, CD.scale, e3, funscale] ; apply mg''
    simp only [F, bound, norm_norm] ; refine (norm_add_le _ _).trans ?_ ; apply add_le_add
    ¬∑ apply (norm_add_le _ _).trans ; simp ; gcongr
    ¬∑ suffices hh1 : |h R v| ‚â§ 1 by simpa using mul_le_mul hh1 le_rfl (by simp) zero_le_one
      simp [h, G, e3, CS.scale, CD.scale, funscale] ; rw [abs_le] ; constructor <;>
      linarith [g.le_one (R‚Åª¬π * v), g.nonneg (R‚Åª¬π * v)]

  have e3 : Integrable bound volume := by refine (Integrable.add ?_ ?_).add ?_ <;> fun_prop

  have e4 : ‚àÄ·µê (a : ‚Ñù), Tendsto (fun n ‚Ü¶ F n a) atTop (ùìù 0) := by
    apply eventually_of_forall ; intro v
    have vR : Tendsto (fun R : ‚Ñù => R‚Åª¬π * v) atTop (ùìù 0) := by simpa using tendsto_inv_atTop_zero.mul_const v
    have evg' : (g : CS 2 ‚Ñù).deriv =·∂†[ùìù 0] 0 := by convert ‚Üê g.zero.deriv ; exact deriv_const' _
    have evg'' : (g : CS 2 ‚Ñù).deriv.deriv =·∂†[ùìù 0] 0 := by convert ‚Üê evg'.deriv ; exact deriv_const' _
    refine tendsto_norm_zero.comp <| (ZeroAtFilter.add ?_ ?_).add ?_
    ¬∑ apply tendsto_nhds_of_eventually_eq
      filter_upwards [vR.eventually evg'', eventually_ne_atTop 0] with R hR hR'
      simp [G'', G', G, CS.deriv_scale, CS.deriv_smul] ; simpa [CS.scale, CD.scale, hR', funscale] using .inl hR
    ¬∑ apply tendsto_nhds_of_eventually_eq ; filter_upwards [vR.eventually evg'] with R hR
      simpa [G', G] using .inl (.inr hR)
    ¬∑ simpa [h] using ((g.tendsto_scale v).const_sub 1).ofReal.mul tendsto_const_nhds

  simpa using tendsto_integral_filter_of_dominated_convergence bound e1 e2 e3 e4
